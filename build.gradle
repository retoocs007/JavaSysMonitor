plugins {
    id 'java'
    id 'war'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'maven-publish'
}

group = 'com.klabazan'
version = '1.1.1'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'com.github.oshi:oshi-core:5.8.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/retoocs007/JavaSysMonitor")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java

            groupId = 'com.klabazan'
            artifactId = 'JavaSysMonitor'
            version = '1.1.1'

            pom {
                name = 'JavaSysMonitor'
                description = 'Small Java SpringBoot REST API service that collects system info'
                url = 'https://github.com/retoocs007/JavaSysMonitor'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id = 'retoocs007'
                        name = 'Krunoslav Labazan'
                        email = 'klabazan@gmail.com'
                    }
                }
            }
        }
    }
}